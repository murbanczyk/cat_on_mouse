#########################################################
# 
# UFDT2
#
# UF D-T2
# Created by Mateusz-Urbanczyk
#
# Copyright (c) Magritek Ltd 2014
#
# U.I. V3
#########################################################

procedure(UFDT2, guipar)

   keaCtrl:checkVersion(2.15,1.1)

   n = :windowdefinition()

   windowvar(wv_dispMode,wv_ctrls,wv_shift,wv_other,wv_rel,
             wv_pp_list,wv_pp_name,wv_var,wv_tabs,wv_phase_list)

   wv_tabs  = ["Pulse Sequence","acquisition","processing+display","File settings"] # Tab names
   wv_ctrls = [2,36,37,60,61,90,91,111,112,121] # Control groups
   wv_shift = [-14,153,320,487,490] # Amount to shift control groups
   wv_other = [120,235,719]  # Collapse control and window size
   wv_saved = "false"

   (wv_rel,wv_var,wv_pp_list,wv_pp_name,wv_phase_list) = :getseqpar()

   if(nrArgs == 0)
      guipar = "show"
   endif

   keaCtrl:setUp(n,"UFDT2",getmacropath(),guipar)
   if(guipar == "hide")
      return(n)
   elseif(guipar == "show")
      try; :initGUI(n); catch; endtry;
      showwindow(n)
      return(n)
   else
      setwindowpar(n,"type","dialog")
      guipar = showdialog(n)
      return(guipar)
   endif

endproc()

#########################################################
# 
# Returns important pulse sequence parameters
#
# rel ......... relationship between pulse sequence parameters
# var ......... variable which change during the pulse sequence
# pp_list ..... list of pulse sequence parameters sent to DSP
# pp_name ..... name of DSP pulse program to run
# phase_list .. phase cycling information
#
#########################################################

procedure(getseqpar)

   rel = ["a1 = 90Amplitude",
          "a2 = 180Amplitude",
          "d1 = pulseLength",
          "d2 = 1000*delta+echoTime/2-d1-2*pgo-CHIRPstep*Tablesize",
          "d3 = echoTime/2-acqTime*500-d1/2+rxLat+echoShift",
          "d33 = echoTime-d1/2+echoShift",
          "d4 = echoTime-d1-pgo-d3",
          "d5 = 1000*delta-d1-2*pgo-CHIRPstep*Tablesize",
          "d6 = CHIRPstep",
          "w1 = 1000*DELTA",
          "n1 = nrEchoes",
          "n2 = nrPnts",
          "n22 = Tablesize",
          "t1 = linvec(1,0,n22)",
          "t2 = linvec(0,0,n22)",
          "totPnts = nrEchoes*nrPnts",
          "totTime = nrEchoes*echoTime"]
   var = ["t1","t2"]
   pp_list = ["a1","p1","d1","t1","t2","p2","n22","d6","d5","p3","w1","p4","p5","d2","n1","l1","a2","p6","d3","n2","d4"]
   pp_name = "UFDT2.p"
   phase_list = [0,0,0,0,0,0,0,0;1,1,1,1,1,1,1,1;2,0,1,3,2,0,1,3;0,2,3,1,0,2,3,1;1,1,2,2,1,1,2,2;1,1,1,1,3,3,3,3;0,0,2,2,0,0,2,2]

endproc(rel,var,pp_list,pp_name,phase_list)


#########################################################
# 
# Provide an interface to this macro which does not
# require the use of the window interface
#
# User must supply a list of parameter (see help file)
# and the pulse program name if different from 
# "$macro$_ext.p"
#
#########################################################

procedure(backdoor, guipar)

   keaCtrl:checkVersion(2.15,1.1)

   (rel,var,pp_list,pp_name,phase_list) = :getseqpar()
  
   (guipar,ppList,pcList,pcIndex,varList) = keaCtrl:initialisePP(getmacropath(),getmacroname(),guipar,rel,var,pp_list,pp_name,phase_list)
   
   r = keaCtrl:runBDPP(guipar,ppList,pcList,pcIndex,varList)

endproc(r)

#########################################################
# 
# Define the window gui
#
#########################################################

procedure(windowdefinition)

   # Automatically generated window definition procedure.
   # Any code added manually will be removed if layout modified interactively
   n = window("UF D-T2", -1, -1, 1048, 716)

      # Define all controls with basic parameters
      getmessage(0,
        keaCtrl:processMessage();)
      tab(1, 6, 7, 1030, 223,
         which = getpar(0,1,"currenttab");
         if(which == 2);
         fn = findobj(0,"name","filter");
         keaCtrl:enableControls(fn+1,fn+3,fn);
         fn = findobj(0,"name","fitType");
         keaCtrl:enableControls(fn+1,fn+8,fn,3);
         endif();)
      groupbox(2, "Pulse sequence", 9, 9, 1022, 160)
      statictext(3, 66, 27, "center", "B1 Frequency (MHz)")
      textbox(4, 43, 42, 46)
      statictext(5, 66, 78, "center", "Repetition time (ms)")
      textbox(6, 43, 93, 46)
      statictext(7, 316, 35, "right", "90 amplitude (dB)")
      textbox(8, 322, 31, 40)
      statictext(9, 316, 61, "right", "180 amplitude (dB)")
      textbox(10, 322, 57, 40)
      statictext(11, 316, 87, "right", "Pulse length (us)")
      textbox(12, 322, 83, 40)
      statictext(13, 566, 35, "right", "Echotime (us)")
      textbox(14, 572, 31, 40)
      statictext(15, 566, 61, "right", "Number of echoes")
      textbox(16, 572, 57, 40)
      statictext(17, 566, 87, "right", "Echo shift (us)")
      textbox(18, 572, 83, 40)
      statictext(19, 566, 139, "right", "Gradient (kHz/mm)")
      textbox(20, 572, 135, 46)
      statictext(21, 316, 113, "right", "CHIRP 180 amplitude (dB)")
      textbox(22, 322, 109, 40)
      statictext(23, 316, 139, "right", "CHIRP step length (us)")
      textbox(24, 322, 135, 40)
      statictext(25, 816, 35, "right", "File with amplitudeTable")
      textbox(26, 822, 31, 200)
      statictext(27, 816, 61, "right", "File with Prospa phase table")
      textbox(28, 822, 57, 200)
      statictext(29, 816, 87, "right", "Size of Amp and Phase tables")
      textbox(30, 822, 83, 40)
      statictext(31, 816, 113, "right", "small delta (ms)")
      textbox(32, 822, 109, 40)
      statictext(33, 816, 139, "right", "DELTA (ms)")
      textbox(34, 822, 135, 40)
      statictext(35, 566, 113, "right", "Auto-phase")
      checkbox(36, 572, 113, "no,yes", "no")
      groupbox(37, "Acquisition", 9, 176, 1022, 160)
      statictext(38, 314, 211, "center", "Rx Gain (dB)")
      textmenu(39, 297, 229, 39, 200)
      statictext(40, 314, 260, "center", "Rx Phase (degrees)")
      textbox(41, 297, 276, 39)
      statictext(42, 422, 213, "center", "Number of")
      statictext(43, 422, 226, "center", "complex points")
      textmenu(44, 469, 215, 62, 200,
         keaCtrl:updateAcqTime();)
      statictext(45, 422, 245, "center", "Dwell time")
      statictext(46, 422, 258, "center", "(us)")
      textmenu(47, 469, 248, 62, 200,
         keaCtrl:updateFilters();)
      statictext(48, 422, 279, "center", "Number of")
      statictext(49, 422, 292, "center", "scans")
      textbox(50, 469, 280, 62)
      statictext(51, 596, 227, "center", "Flat filter")
      checkbox(52, 632, 228, "no,yes", "no",
           keaCtrl:setDwellTimeMenu();
           keaCtrl:updateFilters();)
      statictext(53, 596, 251, "center", "Accumulate")
      checkbox(54, 632, 252, "no,yes", "no")
      statictext(55, 596, 275, "center", "Phase cycle")
      checkbox(56, 632, 277, "no,yes", "yes")
      statictext(57, 724, 211, "center", "Spectral width (kHz)")
      textbox(58, 695, 225, 56)
      statictext(59, 724, 262, "center", "Acquisition time (ms)")
      textbox(60, 695, 276, 56)
      groupbox(61, "Laplace_CPMG", 9, 343, 1022, 160)
      groupbox(62, "", 249, 365, 166, 119)
      statictext(63, 362, 386, "right", "Filter time domain")
      checkbox(64, 370, 387, "no,yes", "no",
           fn = findobj(0,"name","filter");
           keaCtrl:enableControls(fn+1,fn+3,fn);)
      statictext(65, 279, 417, "right", "Filter")
      textmenu(66, 286, 413, 85, 200)
      button(67, 381, 413, 22, 22, "...",
         filters();)
      statictext(68, 362, 449, "right", "Time magnitude")
      checkbox(69, 370, 450, "no,yes", "no")
      groupbox(70, "", 421, 365, 97, 119)
      statictext(71, 458, 391, "center", "Sum echoes")
      checkbox(72, 494, 391, "no,yes", "no")
      statictext(73, 461, 420, "center", "Autophase")
      checkbox(74, 494, 421, "no,yes", "no")
      statictext(75, 462, 449, "center", "Normalize")
      checkbox(76, 494, 451, "no,yes", "no")
      groupbox(77, "", 523, 365, 125, 119)
      statictext(78, 619, 392, "right", "Exponential fit")
      statictext(79, 619, 410, "right", "Bi-exponential fit")
      statictext(80, 619, 428, "right", "Inverse-Laplace fit")
      statictext(81, 619, 446, "right", "No fit")
      radiobuttons(82, 624, 394, 18, "vertical", "exp,biexp,nnls,none", "exp",
           fn = findobj(0,"name","fitType");
           keaCtrl:enableControls(fn+1,fn+8,fn,3);)
      groupbox(83, "", 654, 365, 137, 119)
      statictext(84, 725, 380, "center", "Relaxation range (ms)")
      statictext(85, 713, 406, "right", "Minimum")
      textbox(86, 720, 401, 43)
      statictext(87, 713, 428, "right", "Maximum")
      textbox(88, 720, 424, 43)
      statictext(89, 713, 455, "right", "Alpha")
      textbox(90, 720, 453, 44)
      groupbox(91, "File_Settings", 9, 510, 1022, 160)
      statictext(92, 317, 563, "right", "Working")
      statictext(93, 317, 576, "right", "directory")
      textbox(94, 333, 564, 215)
      button(95, 553, 564, 22, 22, "...",
         keaFiles:setWorkingDir();)
      statictext(96, 324, 600, "right", "Experiment")
      statictext(97, 309, 613, "right", "name")
      textbox(98, 333, 603, 105)
      button(99, 444, 603, 22, 22, "...",
         keaFiles:setExpName();)
      statictext(100, 546, 600, "right", "Experiment")
      statictext(101, 536, 613, "right", "number")
      textbox(102, 554, 603, 25)
      button(103, 584, 603, 22, 22, "...",
         keaFiles:setExpNr();)
      statictext(104, 608, 562, "center", "Save")
      statictext(105, 608, 575, "center", "data")
      checkbox(106, 636, 569, "false,true", "true")
      statictext(107, 652, 600, "center", "Auto")
      statictext(108, 652, 613, "center", "increment")
      checkbox(109, 687, 608, "no,yes", "no")
      button(110, 673, 557, 40, 36, "Load Data",
         keaFiles:loadData();)
      button(111, 728, 557, 40, 50, "Open data folder",
         keaFiles:openDataFolder();)
      colorbox(112, 255, 683, 10, 10, [255,255,255,255])
      colorbox(113, 255, 696, 10, 10, [255,255,255,255])
      button(114, 275, 680, 54, 29, "Run",
         (wv_rel,wv_var,wv_pp_list,wv_pp_name,wv_phase_list) = :getseqpar();
         keaCtrl:runPP(getmacropath(),wv_pp_name);)
      button(115, 340, 680, 54, 29, "Abort",
         setwindowpar(0,"title","Wait for macro to abort ...");
         wvExpStatus = "stop";)
      button(116, 405, 680, 54, 29, "Finish",
         setwindowpar(0,"title","Wait for phase-cycling to complete ...");
         wvExpStatus = "finish";)
      button(117, 470, 680, 54, 29, "Load Par.",
         keaFiles:loadParameters();)
      button(118, 535, 680, 54, 29, "Help",
         help("Macros\UF","UFDT2.htm");)
      button(119, 600, 680, 54, 29, "Kea Pref.",
         keaCtrl:showPreferences();)
      button(120, 665, 680, 54, 29, "Collapse",
         keaCtrl:toggleWindowSize(1);)
      button(121, 730, 680, 54, 29, "Close",
         guipar = getctrlvalues(0);
         if(getwindowpar(0,"type") == "dialog");
         guipar = keaFiles:addWindowSpecifics(guipar);
         keaFiles:saveCommonPar(guipar);
         closedialog(guipar);
         else;
         keaFiles:saveParameters();
         closewindow(0);
         endif;)
      menu(122, "bkgMenu", "Edit macro\tCtrl+Dbl-Click", edit("$getmacroname()$"),
                "Show compiler\tCtrl+Shift+Dbl-Click", MakePulseProgram("$getmacropath()$","$rmext(getmacroname())$"))

     # Set other control parameters
      setpar(n,1,"active","true",
                  "inittabs",[""])
      setpar(n,4,"valueID","b1Freq",
                  "tag","freqRange",
                  "type","double",
                  "range",[1,400])
      setpar(n,6,"valueID","repTime",
                  "type","float",
                  "range",[20,1e+008])
      setpar(n,8,"valueID","90Amplitude",
                  "type","float",
                  "range",[-72,-6])
      setpar(n,10,"valueID","180Amplitude",
                  "type","float",
                  "range",[-85,0])
      setpar(n,12,"valueID","pulseLength",
                  "type","float",
                  "range",[1,500])
      setpar(n,14,"valueID","echoTime",
                  "type","float",
                  "range",[2,327670])
      setpar(n,16,"valueID","nrEchoes",
                  "type","integer",
                  "range",[1,16000])
      setpar(n,18,"valueID","echoShift",
                  "type","float",
                  "range",[-20,20])
      setpar(n,20,"valueID","gradient",
                  "type","float")
      setpar(n,22,"valueID","CHIRPamplitude",
                  "type","float",
                  "range",[-85,0])
      setpar(n,24,"valueID","CHIRPstep",
                  "type","float",
                  "range",[2,1000])
      setpar(n,26,"valueID","AmplitudeTable",
                  "type","string")
      setpar(n,28,"valueID","PhaseTable",
                  "type","string")
      setpar(n,30,"valueID","Tablesize",
                  "type","integer",
                  "range",[1,64000])
      setpar(n,32,"valueID","delta",
                  "type","float",
                  "range",[0.001,100])
      setpar(n,34,"valueID","DELTA",
                  "type","float",
                  "range",[0.001,10000])
      setpar(n,36,"valueID","autoPhase")
      setpar(n,39,"valueID","rxGain",
                  "type","integer",
                  "tooltip","20-40 dB gives optimal SNR.\rDecrease if overload occurs.",
                  "range",[-20,70],
                  "menu",["-20","-17","-14","-11","-8","-5","-2","1","4","7","10","13","16","19","22","25","28","31","34","37","40","43","46","49","52","55","58","61","64","67","70"])
      setpar(n,41,"valueID","rxPhase",
                  "type","float",
                  "tooltip","Applies a fixed phase shift to collected data.\rIgnored if autophasing.")
      setpar(n,44,"valueID","nrPnts",
                  "type","integer",
                  "range",[2,65536],
                  "menu",["2","4","8","16","32","64","128","256","512","1024","2048","4096","8192","16384","32768","65536"])
      setpar(n,47,"valueID","dwellTime",
                  "type","float",
                  "menu",[""])
      setpar(n,50,"valueID","nrScans",
                  "type","integer",
                  "range",[1,1e+006])
      setpar(n,52,"valueID","flatFilter",
                  "tooltip","On: all frequencies equally weighted.\rOff: center frequencies enhanced.")
      setpar(n,54,"valueID","accumulate")
      setpar(n,56,"valueID","usePhaseCycle")
      setpar(n,58,"valueID","bandwidth",
                  "tag","disable")
      setpar(n,60,"valueID","acqTime",
                  "tag","disable")
      setpar(n,64,"valueID","filter")
      setpar(n,66,"valueID","filterType",
                  "menu",[""])
      setpar(n,69,"valueID","timeMag")
      setpar(n,72,"valueID","sumEchoes")
      setpar(n,74,"valueID","autoPhase")
      setpar(n,76,"valueID","normalize")
      setpar(n,82,"valueID","fitType")
      setpar(n,86,"valueID","x_minimum")
      setpar(n,88,"valueID","x_maximum")
      setpar(n,90,"valueID","alpha")
      setpar(n,94,"valueID","dataDirectory",
                  "type","string",
                  "tooltip","Where experimental data will be stored.")
      setpar(n,98,"valueID","expName",
                  "type","string",
                  "tooltip","A subdirectory of the data working directory to store different experiments.")
      setpar(n,102,"valueID","expNr",
                  "tooltip","A numbered subdirectory below the experiment name.\rNot made if entry is blank.")
      setpar(n,106,"valueID","saveData")
      setpar(n,109,"valueID","incExpNr",
                  "tooltip","Increment the experiment number after each experiment.")
      setpar(n,110,"tooltip","Load and display the data specified in the directories fields")
      setpar(n,111,"tooltip","View the data directory in Windows Explorer.")
      setpar(n,112,"objID","ppStatus",
                  "tooltip","Experiment Status Indicator\rGreen: running\rBlue: processing\rYellow: waiting")
      setpar(n,113,"objID","rxOverflow",
                  "tooltip","Receiver Overflow Indicator\rRed: overflow")
      setpar(n,114,"valueID","run",
                  "tooltip","Start the experiment.")
      setpar(n,115,"valueID","stop",
                  "tooltip","Stop the experiment without saving data.",
                  "active","true")
      setpar(n,116,"valueID","finish",
                  "tooltip","Finish the current phase cycle and save data.",
                  "active","true")
      setpar(n,117,"valueID","load",
                  "tooltip","Load a parameter set for a similar experiment.")
      setpar(n,118,"valueID","help",
                  "tooltip","Display HTML help for this experiment.",
                  "active","true")
      setpar(n,119,"valueID","showPref",
                  "tooltip","Display a preferences dialog for the Kea spectrometer.")
      setpar(n,120,"valueID","size",
                  "tooltip","Expand or collapse the window size to show all or fewer controls.",
                  "active","true")
      setpar(n,121,"valueID","close",
                  "tooltip","Close the window saving all parameters.")

     # Set other window parameters
      setwindowpar(n,"bkgmenu",122)

endproc(n)

#######################################################
# Execute the pulse program, collecting nrScans of 
# data and displaying the result in the 1D plot. 
#
# This procedure can be modified to perform more
# complex functions using the passed parameters:
#
# guipar ..... all parameters from the user interface
# ppList ..... the pulse program parameter list
# pcList ..... phase-cycle list
# pcIndex .... indices of phase parameters in ppList
# varIndex ... indices of variable parameters in ppList
#
#######################################################

procedure(execpp,guipar,ppList,pcList,pcIndex,varIndex)

# Make all gui parameters available
   assignlist(guipar)


   if(d1<1|d2<1|d3<1);  
      message("Error","Pulse sequence timing error!","error")
      return(0)
   endif
      if(d4-keaNMR:getAcqTime(guipar) < 1)
      message("Error","Pulse sequence timing error!\rReduce acquisition time or increase echotime.","error")
      return(0)
   endif

# Check for total RF duty cycle
# for PM25----------------------------------------------
       if(pulseLength*nrEchoes/repTime>1.6)
# for PM5----------------------------------------------
        #if(pulseLength*nrEchoes/repTime>3)
    message("Error","RF duty cycle is too high, reduce nrEchoes or increase repTime","error") 
    return(0)
  endif


# Check for p.s. duty cycle should be < 20%
  if(d1/(echoTime) > 0.25)
      message("Error","Duty cycle is too high in pulse sequence!\rshould be < 25%\rIncrease echo time\ror reduce pulse length.","error")
       return(0)
   endif

# Check for too much data
   if(nrEchoes*nrPnts > 128*1024)
      message("Error","Out of DSP memory!\rReduce number of points per echo\ror number of echoes.","error")
       return(0)
   endif

# Calculate a suitable time axis
   t = [0:1:nrPnts-1]/(nrPnts)*acqTime
   f = ([0:1:nrPnts-1]-nrPnts/2+1)/acqTime
   totalTime = ([0:1:totPnts-1]/totPnts)*totTime + echoTime
   

# Allocate data for outputs
   echoData = cmatrix(totPnts)
   echoData2D = cmatrix(nrPnts,nrEchoes)
   ampArray   = matrix(nrEchoes)
   tauArray   = matrix(nrEchoes)
   echoSum    = cmatrix(nrPnts)
   fltTot     = matrix(nrPnts,nrEchoes)
# Filter
       flt = matrix(nrPnts)+1
      fltTot = matrix(totPnts)+1
   
#  subplots
         (prt,pri) = wvPP->multiplot(2,1)
       prn = null



# Reset the timer
   time(0)  

#Setting the amplitude table and sending it to DSP
	import1dpar("ab", "ascii", "xyrc", "real")
	Amplitude=import1d(AmplitudeTable)
	  if(max(Amplitude) > 1.0)
      message("Error","Amplitude table should not contain elements bigger than 1!","error")
       return(0)
   endif
	

    
#Setting the amplitude Phase and sending it to DSP
	Phase=import1d(PhaseTable)
	
	  if(max(Phase) > 2^16)
      message("Error","Phase table is wrong!","error")
       return(0)
   endif
	  if(delta*1e3 < Tablesize*CHIRPstep*2)
      message("Error","small delta should be at least twice the Chirp pulse length!","error")
       return(0)
   endif

# Accumulate scan



   for(scan = 0 to nrScans-1)

    # Note the time at scan start
      t1 = time()   

	        # Set phases for this scan

      (ppList,pAcq) = keaRun:setPPPhase(ppList,scan,pcList,pcIndex)
   
    # Send phase parameters to DSP
      dspwrite("x",0x00,ppList,wvPort)
      		
	Tt1=keaNMR:convertTxGain(CHIRPamplitude)*mag(Amplitude)
    adrs1 = ppList[varIndex[0]] + 1
    dspwrite("y",adrs1,Tt1)
    adrs2 = ppList[varIndex[1]] + 1

    dspwrite("y",adrs2,Phase)
      

    # Collect the data
      (status,data) = keaRun:getData(totPnts,guipar)
      if(status != "ok")
         return(0)
      endif


    # Accumlate the data
      echoData = keaRun:accumulate(accumulate,pAcq,echoData,data)

    # Extract the echoes 
      for(ec = 0 to nrEchoes-1)
            echoData2D[~,ec] = flt.*echoData[ec*nrPnts:(ec+1)*nrPnts-1]/(scan+1)
      next(ec)


   # Autophase first echo if desired
      if(autoPhase == "yes")
        ph = keaRun:autoPhase(echoData,"maxreal")
        ph = ph*pi/180
      else
         ph = 0
      endif

   # Integrate each echo
      for(ec = 0 to nrEchoes-1)
         ampArray[ec]   = sum(real(echoData2D[~,ec]*exp(i*ph)))/(nrPnts)
         tauArray[ec]   = echoTime*(ec+1)
      next(ec)

    # Display full time domain data
         if(sumEchoes == "no") 
            if(nrEchoes == 1)
               keaPlot:graphTime(prt,t*1000,echoData.*fltTot*exp(i*ph),scan,guipar,
                                 "CPMG echo (scan: $scan+1$/$nrScans$)","Acquisition time (\G(m)s)")
            else
               keaPlot:graphTime(prt,totalTime/1000,echoData.*fltTot*exp(i*ph),scan,guipar,
                                 "Raw CPMG echoes (scan: $scan+1$/$nrScans$)","Echo centre time (ms)")
            endif
      # Display all echoes added together
         else 
            for(col = 0 to nrPnts-1)
               echoSum[col] = sum(echoData2D[col,~]')*exp(i*ph)
            next(ec)
            keaPlot:graphTime(prt,
                              t*1000,echoSum,scan,guipar,
                              "CPMG echo sum (scan: $scan+1$/$nrScans$)","Acquisition time (\G(m)s)")
         endif
   
      # Plot the integrals
         if(fitType == "none") # Just display integrals
            guipar = guipar + ["lineMode = \"none\""]
            keaPlot:graphXY(pri,tauArray/1000,ampArray,null,nrEchoes,
                           "Time (ms)","Average echo amplitude (UNITS)",
                           "Echo attenuation data",guipar)
            result = ""
         else # Display integrals with fit after full phase cycle.
            if(scan > 0 & ((scan+1) % size(pcList) == 0)) 
               result = :DisplayFit(pri,prn,tauArray,ampArray,nrEchoes,guipar)
            endif
         endif
   
      
   # Check timing
     check = keaRun:checkTimeAndAbort(guipar,t1,scan,pcList)
     if(check == "abort")
        return(0)
     elseif(check == "finish")
        scan = scan + 1
        exitfor()
     endif


   next(scan)

   # Print result
   if(result != "")
     pr("$result$")
   endif

# Update Rx Phase textbox in GUI
   if(autoPhase == "yes")
      try
         n = findobj(0,"name","rxPhase")
         phOld = getpar(0,n,"value")
         phNew = ((ph*180/pi)+phOld)%360
         setpar(0,n,"text",phNew)
      catch
      endtry
   endif

# Phase the raw echo data
   if(autoPhase == "yes")
      for(ec = 0 to nrEchoes-1)
         echoData2D[~,ec] = echoData2D[~,ec]*exp(i*ph)
      next(ec)
   endif

# Display echo data as 2D plot
 #  :display2DData(echoData2D/nrScans,guipar)

# Save the data
   keaFiles:save1DData(guipar,tauArray,ampArray)
   keaFiles:save2DData(guipar,echoData2D)
   keaFiles:incrementExperiment(guipar)

# Return the data
   result = struct()
   result->tau = tauArray
   result->int = ampArray/nrScans
   result->rawTau = totalTime # ms
   result->rawAmp = 1000*echoData*exp(i*ph)/nrScans # uV

# Return result structure when called from backdoor
   return(result)

endproc("execpp")


###################################################################
#
# Display time domain data in the 2D plot
#
# Parameters are:
# echoData2D ... the data to display
# guipar ....... the window parameters
#
###################################################################
 
procedure(display2DData,echoData2D,guipar)

   assignlist(guipar)

   tmax = acqTime*1000 

   pd = wvPP2->subplot(1,1)

pr (dummyEchoes+1)*nrEchoes*echoTime
   pd->draw("false")
      pd->image(echoData2D,[0,tmax],[0,(dummyEchoes+1)*nrEchoes*echoTime])
      pd->title("CPMG echoes (complex)")
      pd->ylabel("echo time (\G(m)s)")
      pd->xlabel("Acquisition time (\G(m)s)")
   pd->draw("true")

endproc()

###################################################################
#
# Display the passed data points and a fitted t2 curve
#
# Parameters are:
# x ........ the time array
# y ........ the amplitude arrays
# N ........ the number of data points to plot
# guipar ... GUI parameter list
#
###################################################################
 

procedure(DisplayFit,pri,prn,x,y,N,guipar)

  assignlist(guipar)

   if(fitType == "nnls")
      result = :ILFit(pri,prn,x/1000,y,N,guipar)
   else
      result = keaPlot:fitAndGraph(pri,x/1000,y,N,fitType,null,
                          "Time (ms)","Echo integral (UNITS)",
                          "Echo attenuation data",
                          "Fit result: RESULT",guipar)
  endif

endproc(result)


###################################################################
#
# Display the passed data points and a fitted relaxation curve
# and relaxation spectrum
#
# Parameters are:
# x ....... the time array
# y ....... the amplitude arrays
# N ....... the number of data points to plot
# guipar ... GUI parameter list
#
###################################################################
 
procedure(ILFit,pri,prn,x,y,N,guipar)

  assignlist(guipar)

   if(N > 100)
     x_steps = 100    
     (timeDat,yData) = pseudologbin(x,y,100)
   else
     timeDat = x
     yData   = y
     x_steps = N-1
   endif


# Invert meaning of alpha and reduce range
   alpha = 10^(8-log10(alpha))

   guipar = guipar + ["exp_type = \"exp(-t/T)\"",
          "designMat = \"exp(-xData'*(1/xSpec))\"",
          "alpha = $alpha$",
          "logBin = \"true\"",
          "weightBins = \"true\"",
          "x_steps = $x_steps$"]

# Invert using Lawson and Hanson inverstion
   (specX,specY) = LHInvert(x,y,guipar)

# Get fit parameters
  (fit,chisq) = LHInvert:calcFit("exp(-t/T)",specX,specY,x,y)

# Plot original data with L&H fit
   keaPlot:graphXYFit(pri,x,y,fit,null,size(x),
                      "Time (ms)","Average echo amplitude (UNITS)","Echo attenuation data (with IL fit)",guipar)

   guipar = guipar + ["xMap = \"log\"",
                      "traceCol = $wvSpectrumColor$",
                      "symbolShape = \"none\""]

# Plot the spectrum
   keaPlot:graphXY(prn,specX,specY,null,size(specX),
                      "log_(10)(T) ms","Amplitude","Relaxation Spectrum",guipar)


endproc("")
