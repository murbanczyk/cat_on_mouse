#########################################################
# 
# UFT1T2
#
# UF T1-T2 (inversion) Pulse sequence
# Written by: Mateusz Urbanczyk
#
# Copyright (c) Magritek Ltd 2014
#
# U.I. V3
#########################################################

procedure(UFT1T2, guipar)

   keaCtrl:checkVersion(2.15,1.1)

   n = :windowdefinition()

   windowvar(wv_dispMode,wv_ctrls,wv_shift,wv_other,wv_rel,
             wv_pp_list,wv_pp_name,wv_var,wv_tabs,wv_phase_list)

   wv_tabs  = ["Pulse Sequence","acquisition","processing+display","File settings"] # Tab names
   wv_ctrls = [2,32,33,56,57,86,87,107,108,117] # Control groups
   wv_shift = [-14,153,320,487,490] # Amount to shift control groups
   wv_other = [116,235,719]  # Collapse control and window size
   wv_saved = "false"

   (wv_rel,wv_var,wv_pp_list,wv_pp_name,wv_phase_list) = :getseqpar()

   if(nrArgs == 0)
      guipar = "show"
   endif

   keaCtrl:setUp(n,"UFT1T2",getmacropath(),guipar)
   if(guipar == "hide")
      return(n)
   elseif(guipar == "show")
      try; :initGUI(n); catch; endtry;
      showwindow(n)
      return(n)
   else
      setwindowpar(n,"type","dialog")
      guipar = showdialog(n)
      return(guipar)
   endif

endproc()

#########################################################
# 
# Returns important pulse sequence parameters
#
# rel ......... relationship between pulse sequence parameters
# var ......... variable which change during the pulse sequence
# pp_list ..... list of pulse sequence parameters sent to DSP
# pp_name ..... name of DSP pulse program to run
# phase_list .. phase cycling information
#
#########################################################

procedure(getseqpar)

   rel = ["a1 = 90Amplitude",
          "a2 = 180Amplitude",
          "a3 = CHIRPamplitude",
          "d5 = CHIRPstep",
          "d1 = pulseLength",
          "d2 = echoTime/2-d1-pgo",
          "d33 = chirpdel",
          "d3 = echoTime/2-acqTime*500-d1/2+rxLat+echoShift",
          "d4 = echoTime-d1-pgo-d3",
          "n1 = nrEchoes",
          "n2 = nrPnts",
          "n22 = Tablesize",
          "n333 = (2*n22+nrPnts*nrEchoes)",
          "t1 = linvec(1,0,n22)",
          "t2 = linvec(0,0,n22)",
          "totPnts = nrPnts*nrEchoes",
          "totTime = nrEchoes*echoTime+ n22*d5"]
   var = ["t1","t2"]
   pp_list = ["n333","t1","t2","p1","n22","d5","d33","a1","p2","d1","d2","n1","l1","a2","p3","d3","n2","d4"]
   pp_name = "UFT1T2.p"
   phase_list = [0,0,0,0,2,2,2,2;0,2,0,2,0,2,0,2;1,1,3,3,1,1,3,3;0,2,0,2,0,2,0,2]

endproc(rel,var,pp_list,pp_name,phase_list)


#########################################################
# 
# Provide an interface to this macro which does not
# require the use of the window interface
#
# User must supply a list of parameter (see help file)
# and the pulse program name if different from 
# "$macro$_ext.p"
#
#########################################################

procedure(backdoor, guipar)

   keaCtrl:checkVersion(2.15,1.1)

   (rel,var,pp_list,pp_name,phase_list) = :getseqpar()
  
   (guipar,ppList,pcList,pcIndex,varList) = keaCtrl:initialisePP(getmacropath(),getmacroname(),guipar,rel,var,pp_list,pp_name,phase_list)
   
   r = keaCtrl:runBDPP(guipar,ppList,pcList,pcIndex,varList)

endproc(r)

#########################################################
# 
# Define the window gui
#
#########################################################

procedure(windowdefinition)

   # Automatically generated window definition procedure.
   # Any code added manually will be removed if layout modified interactively
   n = window("UF t1t2", -1, -1, 898, 716)

      # Define all controls with basic parameters
      getmessage(0,
        keaCtrl:processMessage();)
      tab(1, 6, 7, 880, 223,
         which = getpar(0,1,"currenttab");
         if(which == 2);
         fn = findobj(0,"name","filter");
         keaCtrl:enableControls(fn+1,fn+3,fn);
         fn = findobj(0,"name","fitType");
         keaCtrl:enableControls(fn+1,fn+8,fn,3);
         endif();)
      groupbox(2, "Pulse sequence", 9, 9, 872, 160)
      statictext(3, 66, 27, "center", "B1 Frequency (MHz)")
      textbox(4, 43, 42, 46)
      statictext(5, 66, 67, "center", "Repetition time (ms)")
      textbox(6, 43, 82, 46)
      statictext(7, 266, 35, "right", "90 amplitude (dB)")
      textbox(8, 272, 31, 40)
      statictext(9, 266, 61, "right", "180 amplitude (dB)")
      textbox(10, 272, 57, 40)
      statictext(11, 266, 139, "right", "Pulse length (us)")
      textbox(12, 272, 135, 40)
      statictext(13, 266, 87, "right", "CHIRP 180 amplitude (dB)")
      textbox(14, 272, 83, 40)
      statictext(15, 266, 113, "right", "CHIRP step length (us)")
      textbox(16, 272, 109, 40)
      statictext(17, 666, 35, "right", "File with amplitudeTable")
      textbox(18, 672, 31, 200)
      statictext(19, 666, 61, "right", "File with Prospa phase table")
      textbox(20, 672, 57, 200)
      statictext(21, 466, 35, "right", "Echotime (us)")
      textbox(22, 472, 31, 40)
      statictext(23, 466, 113, "right", "After chirp delay (us)")
      textbox(24, 472, 109, 40)
      statictext(25, 466, 61, "right", "Number of echoes")
      textbox(26, 472, 57, 40)
      statictext(27, 666, 87, "right", "Size of Amp and Phase tables")
      textbox(28, 672, 83, 40)
      statictext(29, 466, 87, "right", "Echo shift (us)")
      textbox(30, 472, 83, 40)
      statictext(31, 466, 139, "right", "Auto-phase")
      checkbox(32, 472, 139, "no,yes", "no")
      groupbox(33, "Acquisition", 9, 176, 872, 160)
      statictext(34, 239, 211, "center", "Rx Gain (dB)")
      textmenu(35, 222, 229, 39, 200)
      statictext(36, 239, 260, "center", "Rx Phase (degrees)")
      textbox(37, 222, 276, 39)
      statictext(38, 347, 213, "center", "Number of")
      statictext(39, 347, 226, "center", "complex points")
      textmenu(40, 394, 215, 62, 200,
         keaCtrl:updateAcqTime();)
      statictext(41, 347, 245, "center", "Dwell time")
      statictext(42, 347, 258, "center", "(us)")
      textmenu(43, 394, 248, 62, 200,
         keaCtrl:updateFilters();)
      statictext(44, 347, 279, "center", "Number of")
      statictext(45, 347, 292, "center", "scans")
      textbox(46, 394, 280, 62)
      statictext(47, 521, 227, "center", "Flat filter")
      checkbox(48, 557, 228, "no,yes", "no",
           keaCtrl:setDwellTimeMenu();
           keaCtrl:updateFilters();)
      statictext(49, 521, 251, "center", "Accumulate")
      checkbox(50, 557, 252, "no,yes", "no")
      statictext(51, 521, 275, "center", "Phase cycle")
      checkbox(52, 557, 277, "no,yes", "yes")
      statictext(53, 649, 211, "center", "Spectral width (kHz)")
      textbox(54, 620, 225, 56)
      statictext(55, 649, 262, "center", "Acquisition time (ms)")
      textbox(56, 620, 276, 56)
      groupbox(57, "Laplace_CPMG", 9, 343, 872, 160)
      groupbox(58, "", 174, 365, 166, 119)
      statictext(59, 287, 386, "right", "Filter time domain")
      checkbox(60, 295, 387, "no,yes", "no",
           fn = findobj(0,"name","filter");
           keaCtrl:enableControls(fn+1,fn+3,fn);)
      statictext(61, 204, 417, "right", "Filter")
      textmenu(62, 211, 413, 85, 200)
      button(63, 306, 413, 22, 22, "...",
         filters();)
      statictext(64, 287, 449, "right", "Time magnitude")
      checkbox(65, 295, 450, "no,yes", "no")
      groupbox(66, "", 346, 365, 97, 119)
      statictext(67, 383, 391, "center", "Sum echoes")
      checkbox(68, 419, 391, "no,yes", "no")
      statictext(69, 386, 420, "center", "Autophase")
      checkbox(70, 419, 421, "no,yes", "no")
      statictext(71, 387, 449, "center", "Normalize")
      checkbox(72, 419, 451, "no,yes", "no")
      groupbox(73, "", 448, 365, 125, 119)
      statictext(74, 544, 392, "right", "Exponential fit")
      statictext(75, 544, 410, "right", "Bi-exponential fit")
      statictext(76, 544, 428, "right", "Inverse-Laplace fit")
      statictext(77, 544, 446, "right", "No fit")
      radiobuttons(78, 549, 394, 18, "vertical", "exp,biexp,nnls,none", "exp",
           fn = findobj(0,"name","fitType");
           keaCtrl:enableControls(fn+1,fn+8,fn,3);)
      groupbox(79, "", 579, 365, 137, 119)
      statictext(80, 650, 380, "center", "Relaxation range (ms)")
      statictext(81, 638, 406, "right", "Minimum")
      textbox(82, 645, 401, 43)
      statictext(83, 638, 428, "right", "Maximum")
      textbox(84, 645, 424, 43)
      statictext(85, 638, 455, "right", "Alpha")
      textbox(86, 645, 453, 44)
      groupbox(87, "File_Settings", 9, 510, 872, 160)
      statictext(88, 242, 563, "right", "Working")
      statictext(89, 242, 576, "right", "directory")
      textbox(90, 258, 564, 215)
      button(91, 478, 564, 22, 22, "...",
         keaFiles:setWorkingDir();)
      statictext(92, 249, 600, "right", "Experiment")
      statictext(93, 234, 613, "right", "name")
      textbox(94, 258, 603, 105)
      button(95, 369, 603, 22, 22, "...",
         keaFiles:setExpName();)
      statictext(96, 471, 600, "right", "Experiment")
      statictext(97, 461, 613, "right", "number")
      textbox(98, 479, 603, 25)
      button(99, 509, 603, 22, 22, "...",
         keaFiles:setExpNr();)
      statictext(100, 533, 562, "center", "Save")
      statictext(101, 533, 575, "center", "data")
      checkbox(102, 561, 569, "false,true", "true")
      statictext(103, 577, 600, "center", "Auto")
      statictext(104, 577, 613, "center", "increment")
      checkbox(105, 612, 608, "no,yes", "no")
      button(106, 598, 557, 40, 36, "Load Data",
         keaFiles:loadData();)
      button(107, 653, 557, 40, 50, "Open data folder",
         keaFiles:openDataFolder();)
      colorbox(108, 180, 683, 10, 10, [255,255,255,255])
      colorbox(109, 180, 696, 10, 10, [255,255,255,255])
      button(110, 200, 680, 54, 29, "Run",
         (wv_rel,wv_var,wv_pp_list,wv_pp_name,wv_phase_list) = :getseqpar();
         keaCtrl:runPP(getmacropath(),wv_pp_name);)
      button(111, 265, 680, 54, 29, "Abort",
         setwindowpar(0,"title","Wait for macro to abort ...");
         wvExpStatus = "stop";)
      button(112, 330, 680, 54, 29, "Finish",
         setwindowpar(0,"title","Wait for phase-cycling to complete ...");
         wvExpStatus = "finish";)
      button(113, 395, 680, 54, 29, "Load Par.",
         keaFiles:loadParameters();)
      button(114, 460, 680, 54, 29, "Help",
         help("Macros\UF","UFT1T2.htm");)
      button(115, 525, 680, 54, 29, "Kea Pref.",
         keaCtrl:showPreferences();)
      button(116, 590, 680, 54, 29, "Collapse",
         keaCtrl:toggleWindowSize(1);)
      button(117, 655, 680, 54, 29, "Close",
         guipar = getctrlvalues(0);
         if(getwindowpar(0,"type") == "dialog");
         guipar = keaFiles:addWindowSpecifics(guipar);
         keaFiles:saveCommonPar(guipar);
         closedialog(guipar);
         else;
         keaFiles:saveParameters();
         closewindow(0);
         endif;)
      menu(118, "bkgMenu", "Edit macro\tCtrl+Dbl-Click", edit("$getmacroname()$"),
                "Show compiler\tCtrl+Shift+Dbl-Click", MakePulseProgram("$getmacropath()$","$rmext(getmacroname())$"))

     # Set other control parameters
      setpar(n,1,"active","true",
                  "inittabs",[""])
      setpar(n,4,"valueID","b1Freq",
                  "tag","freqRange",
                  "type","double",
                  "range",[1,400])
      setpar(n,6,"valueID","repTime",
                  "type","float",
                  "range",[20,1e+008])
      setpar(n,8,"valueID","90Amplitude",
                  "type","float",
                  "range",[-85,0])
      setpar(n,10,"valueID","180Amplitude",
                  "type","float",
                  "range",[-85,0])
      setpar(n,12,"valueID","pulseLength",
                  "type","float",
                  "range",[2,500])
      setpar(n,14,"valueID","CHIRPamplitude",
                  "type","float",
                  "range",[-85,0])
      setpar(n,16,"valueID","CHIRPstep",
                  "type","float",
                  "range",[2,1000])
      setpar(n,18,"valueID","AmplitudeTable",
                  "type","string")
      setpar(n,20,"valueID","PhaseTable",
                  "type","string")
      setpar(n,22,"valueID","echoTime",
                  "type","float",
                  "range",[2,327670])
      setpar(n,24,"valueID","chirpdel",
                  "type","float",
                  "range",[2,327670])
      setpar(n,26,"valueID","nrEchoes",
                  "type","integer",
                  "range",[1,8192])
      setpar(n,28,"valueID","Tablesize",
                  "type","integer",
                  "range",[1,64000])
      setpar(n,30,"valueID","echoShift",
                  "type","float",
                  "range",[-20,20])
      setpar(n,32,"valueID","autoPhase")
      setpar(n,35,"valueID","rxGain",
                  "type","integer",
                  "tooltip","20-40 dB gives optimal SNR.\rDecrease if overload occurs.",
                  "range",[-20,70],
                  "menu",["-20","-17","-14","-11","-8","-5","-2","1","4","7","10","13","16","19","22","25","28","31","34","37","40","43","46","49","52","55","58","61","64","67","70"])
      setpar(n,37,"valueID","rxPhase",
                  "type","float",
                  "tooltip","Applies a fixed phase shift to collected data.\rIgnored if autophasing.")
      setpar(n,40,"valueID","nrPnts",
                  "type","integer",
                  "range",[2,65536],
                  "menu",["2","4","8","16","32","64","128","256","512","1024","2048","4096","8192","16384","32768","65536"])
      setpar(n,43,"valueID","dwellTime",
                  "type","float",
                  "menu",[""])
      setpar(n,46,"valueID","nrScans",
                  "type","integer",
                  "range",[1,1e+006])
      setpar(n,48,"valueID","flatFilter",
                  "tooltip","On: all frequencies equally weighted.\rOff: center frequencies enhanced.")
      setpar(n,50,"valueID","accumulate")
      setpar(n,52,"valueID","usePhaseCycle")
      setpar(n,54,"valueID","bandwidth",
                  "tag","disable")
      setpar(n,56,"valueID","acqTime",
                  "tag","disable")
      setpar(n,60,"valueID","filter")
      setpar(n,62,"valueID","filterType",
                  "menu",[""])
      setpar(n,65,"valueID","timeMag")
      setpar(n,68,"valueID","sumEchoes")
      setpar(n,70,"valueID","autoPhase")
      setpar(n,72,"valueID","normalize")
      setpar(n,78,"valueID","fitType")
      setpar(n,82,"valueID","x_minimum")
      setpar(n,84,"valueID","x_maximum")
      setpar(n,86,"valueID","alpha")
      setpar(n,90,"valueID","dataDirectory",
                  "type","string",
                  "tooltip","Where experimental data will be stored.")
      setpar(n,94,"valueID","expName",
                  "type","string",
                  "tooltip","A subdirectory of the data working directory to store different experiments.")
      setpar(n,98,"valueID","expNr",
                  "tooltip","A numbered subdirectory below the experiment name.\rNot made if entry is blank.")
      setpar(n,102,"valueID","saveData")
      setpar(n,105,"valueID","incExpNr",
                  "tooltip","Increment the experiment number after each experiment.")
      setpar(n,106,"tooltip","Load and display the data specified in the directories fields")
      setpar(n,107,"tooltip","View the data directory in Windows Explorer.")
      setpar(n,108,"objID","ppStatus",
                  "tooltip","Experiment Status Indicator\rGreen: running\rBlue: processing\rYellow: waiting")
      setpar(n,109,"objID","rxOverflow",
                  "tooltip","Receiver Overflow Indicator\rRed: overflow")
      setpar(n,110,"valueID","run",
                  "tooltip","Start the experiment.")
      setpar(n,111,"valueID","stop",
                  "tooltip","Stop the experiment without saving data.",
                  "active","true")
      setpar(n,112,"valueID","finish",
                  "tooltip","Finish the current phase cycle and save data.",
                  "active","true")
      setpar(n,113,"valueID","load",
                  "tooltip","Load a parameter set for a similar experiment.")
      setpar(n,114,"valueID","help",
                  "tooltip","Display HTML help for this experiment.",
                  "active","true")
      setpar(n,115,"valueID","showPref",
                  "tooltip","Display a preferences dialog for the Kea spectrometer.")
      setpar(n,116,"valueID","size",
                  "tooltip","Expand or collapse the window size to show all or fewer controls.",
                  "active","true")
      setpar(n,117,"valueID","close",
                  "tooltip","Close the window saving all parameters.")

     # Set other window parameters
      setwindowpar(n,"bkgmenu",118)

endproc(n)

#######################################################
# Execute the pulse program, collecting nrScans of 
# data and displaying the result in the 1D plot. 
#
# This procedure can be modified to perform more
# complex functions using the passed parameters:
#
# guipar ..... all parameters from the user interface
# ppList ..... the pulse program parameter list
# pcList ..... phase-cycle list
# pcIndex .... indices of phase parameters in ppList
# varIndex ... indices of variable parameters in ppList
#
#######################################################

procedure(execpp,guipar,ppList,pcList,pcIndex,varIndex)

# Make all gui parameters available
   assignlist(guipar)


   if(d1<1|d2<1|d3<1);  
      message("Error","Pulse sequence timing error!","error")
      return(0)
   endif
      if(d4-keaNMR:getAcqTime(guipar) < 1)
      message("Error","Pulse sequence timing error!\rReduce acquisition time or increase echotime.","error")
      return(0)
   endif

# Check for total RF duty cycle
# for PM25----------------------------------------------
       if(pulseLength*nrEchoes/repTime>1.6)
# for PM5----------------------------------------------
        #if(pulseLength*nrEchoes/repTime>3)
    message("Error","RF duty cycle is too high, reduce nrEchoes or increase repTime","error") 
    return(0)
  endif


# Check for p.s. duty cycle should be < 20%
  if(d1/(echoTime) > 0.25)
      message("Error","Duty cycle is too high in pulse sequence!\rshould be < 25%\rIncrease echo time\ror reduce pulse length.","error")
       return(0)
   endif

# Check for too much data
   if(nrEchoes*nrPnts > 128*1024)
      message("Error","Out of DSP memory!\rReduce number of points per echo\ror number of echoes.","error")
       return(0)
   endif

# Calculate a suitable time axis
   t = [0:1:nrPnts-1]/(nrPnts)*acqTime
   f = ([0:1:nrPnts-1]-nrPnts/2+1)/acqTime
   totalTime = ([0:1:totPnts-1]/totPnts)*totTime + echoTime
   

# Allocate data for outputs
   echoData = cmatrix(totPnts)
   echoData2D = cmatrix(nrPnts,nrEchoes)
   ampArray   = matrix(nrEchoes)
   tauArray   = matrix(nrEchoes)
   echoSum    = cmatrix(nrPnts)
   fltTot     = matrix(nrPnts,nrEchoes)
# Filter
   if(filter == "yes")
      flt = filters:get_filter(filterType,"FTEcho",nrPnts)
      for(ec = 0 to nrEchoes-1)
         fltTot[~,ec] = flt
      next(ec)
      fltTot = reshape(fltTot,totPnts,1)
   else
      flt = matrix(nrPnts)+1
      fltTot = matrix(totPnts)+1
   endif
#  subplots
   if(fitType == "nnls")
      (prt,pri,prn) = wvPP->multiplot(3,1)
   else
      (prt,pri) = wvPP->multiplot(2,1)
       prn = null
   endif


# Reset the timer
   time(0)  

#Setting the amplitude table and sending it to DSP
	import1dpar("ab", "ascii", "xyrc", "real")
	Amplitude=import1d(AmplitudeTable)
	  if(max(Amplitude) > 1.0)
      message("Error","Amplitude table should not contain elements bigger than 1!","error")
       return(0)
   endif
	

    
#Setting the amplitude Phase and sending it to DSP
	Phase=import1d(PhaseTable)
	
	  if(max(Phase) > 2^16)
      message("Error","Phase table is wrong!","error")
       return(0)
   endif
	


# Accumulate scan



   for(scan = 0 to nrScans-1)

    # Note the time at scan start
      t1 = time()   

	        # Set phases for this scan

      (ppList,pAcq) = keaRun:setPPPhase(ppList,scan,pcList,pcIndex)
   
    # Send phase parameters to DSP
      dspwrite("x",0x00,ppList,wvPort)
      		
	Tt1=keaNMR:convertTxGain(CHIRPamplitude)*mag(Amplitude)
    adrs1 = ppList[varIndex[0]] + 1
    dspwrite("y",adrs1,Tt1)
    adrs2 = ppList[varIndex[1]] + 1

    dspwrite("y",adrs2,Phase)
      

    # Collect the data
      (status,data) = keaRun:getData(totPnts,guipar)
      if(status != "ok")
         return(0)
      endif


    # Accumlate the data
      echoData = keaRun:accumulate(accumulate,pAcq,echoData,data)

    # Extract the echoes 
      for(ec = 0 to nrEchoes-1)
            echoData2D[~,ec] = flt.*echoData[ec*nrPnts:(ec+1)*nrPnts-1]/(scan+1)
      next(ec)


   # Autophase first echo if desired
      if(autoPhase == "yes")
        ph = keaRun:autoPhase(echoData,"maxreal")
        ph = ph*pi/180
      else
         ph = 0
      endif

   # Integrate each echo
      for(ec = 0 to nrEchoes-1)
         ampArray[ec]   = sum(real(echoData2D[~,ec]*exp(i*ph)))/(nrPnts)
         tauArray[ec]   = echoTime*(ec+1)
      next(ec)

    # Display full time domain data
         if(sumEchoes == "no") 
            if(nrEchoes == 1)
               keaPlot:graphTime(prt,t*1000,echoData.*fltTot*exp(i*ph),scan,guipar,
                                 "CPMG echo (scan: $scan+1$/$nrScans$)","Acquisition time (\G(m)s)")
            else
               keaPlot:graphTime(prt,totalTime/1000,echoData.*fltTot*exp(i*ph),scan,guipar,
                                 "Raw CPMG echoes (scan: $scan+1$/$nrScans$)","Echo centre time (ms)")
            endif
      # Display all echoes added together
         else 
            for(col = 0 to nrPnts-1)
               echoSum[col] = sum(echoData2D[col,~]')*exp(i*ph)
            next(ec)
            keaPlot:graphTime(prt,
                              t*1000,echoSum,scan,guipar,
                              "CPMG echo sum (scan: $scan+1$/$nrScans$)","Acquisition time (\G(m)s)")
         endif
   
      # Plot the integrals
         if(fitType == "none") # Just display integrals
            guipar = guipar + ["lineMode = \"none\""]
            keaPlot:graphXY(pri,tauArray/1000,ampArray,null,nrEchoes,
                           "Time (ms)","Average echo amplitude (UNITS)",
                           "Echo attenuation data",guipar)
            result = ""
         else # Display integrals with fit after full phase cycle.
            if(scan > 0 & ((scan+1) % size(pcList) == 0)) 
               result = :DisplayFit(pri,prn,tauArray,ampArray,nrEchoes,guipar)
            endif
         endif
   
      
   # Check timing
     check = keaRun:checkTimeAndAbort(guipar,t1,scan,pcList)
     if(check == "abort")
        return(0)
     elseif(check == "finish")
        scan = scan + 1
        exitfor()
     endif


   next(scan)

   # Print result
   if(result != "")
     pr("$result$")
   endif

# Update Rx Phase textbox in GUI
   if(autoPhase == "yes")
      try
         n = findobj(0,"name","rxPhase")
         phOld = getpar(0,n,"value")
         phNew = ((ph*180/pi)+phOld)%360
         setpar(0,n,"text",phNew)
      catch
      endtry
   endif

# Phase the raw echo data
   if(autoPhase == "yes")
      for(ec = 0 to nrEchoes-1)
         echoData2D[~,ec] = echoData2D[~,ec]*exp(i*ph)
      next(ec)
   endif

# Display echo data as 2D plot
 #  :display2DData(echoData2D/nrScans,guipar)

# Save the data
   keaFiles:save1DData(guipar,tauArray,ampArray)
   keaFiles:save2DData(guipar,echoData2D)
   keaFiles:incrementExperiment(guipar)

# Return the data
   result = struct()
   result->tau = tauArray
   result->int = ampArray/nrScans
   result->rawTau = totalTime # ms
   result->rawAmp = 1000*echoData*exp(i*ph)/nrScans # uV

# Return result structure when called from backdoor
   return(result)

endproc("execpp")


###################################################################
#
# Display time domain data in the 2D plot
#
# Parameters are:
# echoData2D ... the data to display
# guipar ....... the window parameters
#
###################################################################
 
procedure(display2DData,echoData2D,guipar)

   assignlist(guipar)

   tmax = acqTime*1000 

   pd = wvPP2->subplot(1,1)

pr (dummyEchoes+1)*nrEchoes*echoTime
   pd->draw("false")
      pd->image(echoData2D,[0,tmax],[0,(dummyEchoes+1)*nrEchoes*echoTime])
      pd->title("CPMG echoes (complex)")
      pd->ylabel("echo time (\G(m)s)")
      pd->xlabel("Acquisition time (\G(m)s)")
   pd->draw("true")

endproc()

###################################################################
#
# Display the passed data points and a fitted t2 curve
#
# Parameters are:
# x ........ the time array
# y ........ the amplitude arrays
# N ........ the number of data points to plot
# guipar ... GUI parameter list
#
###################################################################
 

procedure(DisplayFit,pri,prn,x,y,N,guipar)

  assignlist(guipar)

   if(fitType == "nnls")
      result = :ILFit(pri,prn,x/1000,y,N,guipar)
   else
      result = keaPlot:fitAndGraph(pri,x/1000,y,N,fitType,null,
                          "Time (ms)","Echo integral (UNITS)",
                          "Echo attenuation data",
                          "Fit result: RESULT",guipar)
  endif

endproc(result)


###################################################################
#
# Display the passed data points and a fitted relaxation curve
# and relaxation spectrum
#
# Parameters are:
# x ....... the time array
# y ....... the amplitude arrays
# N ....... the number of data points to plot
# guipar ... GUI parameter list
#
###################################################################
 
procedure(ILFit,pri,prn,x,y,N,guipar)

  assignlist(guipar)

   if(N > 100)
     x_steps = 100    
     (timeDat,yData) = pseudologbin(x,y,100)
   else
     timeDat = x
     yData   = y
     x_steps = N-1
   endif


# Invert meaning of alpha and reduce range
   alpha = 10^(8-log10(alpha))

   guipar = guipar + ["exp_type = \"exp(-t/T)\"",
          "designMat = \"exp(-xData'*(1/xSpec))\"",
          "alpha = $alpha$",
          "logBin = \"true\"",
          "weightBins = \"true\"",
          "x_steps = $x_steps$"]

# Invert using Lawson and Hanson inverstion
   (specX,specY) = LHInvert(x,y,guipar)

# Get fit parameters
  (fit,chisq) = LHInvert:calcFit("exp(-t/T)",specX,specY,x,y)

# Plot original data with L&H fit
   keaPlot:graphXYFit(pri,x,y,fit,null,size(x),
                      "Time (ms)","Average echo amplitude (UNITS)","Echo attenuation data (with IL fit)",guipar)

   guipar = guipar + ["xMap = \"log\"",
                      "traceCol = $wvSpectrumColor$",
                      "symbolShape = \"none\""]

# Plot the spectrum
   keaPlot:graphXY(prn,specX,specY,null,size(specX),
                      "log_(10)(T) ms","Amplitude","Relaxation Spectrum",guipar)


endproc("")
